import 'OCCI.ecore'

package OCCI

--
-- Semantics of the core concepts.
--

--
-- Category.identity
--

context Category
def : identity : String = scheme + term

context Category
inv IdentityUnique:
allInstances()->isUnique(identity)
-- see tests/NoKindTermUnicity.xmi
 
--
-- Category.scheme
--

context Category
inv SchemeEndsWithSharp('The scheme "' + scheme + '" must end with a sharp'):
scheme.substring(scheme.size(),scheme.size()) = '#'
-- see tests/CategorySchemeNotEndsWithSharp.xmi

context Action
def : category : Category = oclContainer().oclAsType(Category)

context Action
inv CorrectScheme:
scheme = category.scheme.substring(1,category.scheme.size()-1) + '/' + category.term + '/action#'
-- see tests/IncorrectKindActionScheme.xmi
-- see tests/IncorrectMixinActionScheme.xmi

--
-- Category.attributes.name
--

context Category
inv AttributesNameUnique('Some attributes have the same name'):
attributes->isUnique(name)
-- see tests/NoAttributeNameUnicity.xmi

--
-- Mixin
--

context Mixin
def : isTag : Boolean = attributes->isEmpty() and actions->isEmpty() and depends->forAll(isTag)

context Mixin
def : isTemplate : Boolean = attributes->forAll(default <> null) and depends->forAll(isTag or isTemplate)

--
-- Entity.id
--

context Entity
inv IdUnique:
allInstances()->isUnique(id)
-- see tests/NoUniqueEntityId.xmi

--
-- Semantics of the type classification system.
--

context Kind
inv NoCyclicInheritance: 
parent->closure(parent)->excludes(self)
-- see tests/InvalidKindInheritance.xmi

-- NEW
context Kind
def : entityKind : Kind = Kind.allInstances()->one(identity = 'http://schemas.ogf.org/occi/core#entity' and parent = null)

context Kind
inv EntityKindIsRootParent:
self->closure(parent)->exists(k | k.identity = 'http://schemas.ogf.org/occi/core#entity' and k.parent = null)
-- see tests/EntityKindNotRootParent.xmi

context Kind
inv AttributesNameNotAlreadyDefinedInParent:
attributes.name->excludesAll(parent->closure(parent).attributes.name)
-- see tests/KindAttributeAlreadyDefined.xmi

context Mixin
inv NoCyclicInheritance:
depends->closure(depends)->excludes(self)
-- see tests/InvalidMixinInheritance.xmi

context Mixin
inv AttributesNameNotAlreadyDefinedInDepends:
attributes.name->excludesAll(depends->closure(depends).attributes.name)
-- see tests/MixinAttributeAlreadyDefinedInDepends

context Entity
inv KindCompatibleWithOneAppliesOfEachMixin:
mixins->forAll(m | m.applies->notEmpty() implies m.applies->exists(k | kind->closure(parent)->includes(k)))
-- see tests/ResourceWithIncorrectMixin.xmi

-- NEW
context Resource
def : resourceKind : Kind = Kind.allInstances()->one(identity = 'http://schemas.ogf.org/occi/core#resource' and parent = entityKind)

context Resource
inv ResourceKindIsInParent:
-- OLD kind->closure(parent)->exists(k | k.identity = 'http://schemas.ogf.org/occi/core#resource')
kind->closure(parent)->includes(resourceKind)
-- see tests/ResourceKindNotRootKind.xmi

-- NEW
context Link
def : linkKind : Kind = Kind.allInstances()->one(identity = 'http://schemas.ogf.org/occi/core#link' and parent = entityKind)

context Link
inv LinkKindIsInParent:
-- OLD kind->closure(parent)->exists(k | k.identity =  'http://schemas.ogf.org/occi/core#link')
kind->closure(parent)->includes(linkKind)
-- see tests/LinkKindNotRootKind.xmi

--
-- Semantics of Extension.
--

context Extension
inv SchemeUnique:
allInstances()->isUnique(scheme)
-- TODO: a test case

context Extension
inv KindsSchemeValid:
kinds->forAll(k | k.scheme = scheme)
-- see tests/InvalidKindScheme.xmi

context Extension
inv MixinsSchemeValid:
mixins->forAll(m | m.scheme.substring(1,scheme.size()-1) = scheme.substring(1,scheme.size()-1))
-- see tests/InvalidMixinScheme.xmi

context Extension
inv NoCycleInImportation:
import->closure(import)->excludes(self)
-- see tests/CycleInImportation.xmi

context Category
def : extension : Extension = oclContainer().oclAsType(Extension)

-- NEW
context Extension
def : isOwnedOrImported(category : Category) : Boolean = category.extension = self or import->includes(category.extension)
 
context Extension
inv KindParentLocalOrImported:
-- OLD kinds.parent->forAll(parent.extension = self or import->includes(parent.extension))
kinds->forAll(parent <> null implies isOwnedOrImported(parent))
-- see tests/KindParentNotImported.xmi

context Extension
inv MixinDependsLocalOrImported:
mixins.depends->forAll(k | isOwnedOrImported(k))
-- see tests/MixinDependsNotImported.xmi

context Extension
inv MixinAppliesLocalOrImported:
mixins.applies->forAll(k | isOwnedOrImported(k))
-- see tests/MixinAppliesNotImported.xmi

--
-- Semantics of Configuration.
--

context Configuration
inv AllResourcesKindInUse:
use->includesAll(resources.kind.extension)
-- see tests/ResourceKindNotInUse.xmi

context Configuration
inv AllResourcesMixinsInUse:
use->includesAll(resources.mixins.extension)
-- see tests/ResourceMixinNotInUse.xmi

context Configuration
inv AllResourcesLinksKindInUse:
use->includesAll(resources.links.kind.extension)
-- see tests/LinkKindNotInUse.xmi

context Configuration
inv AllResourcesLinksMixinsInUse:
use->includesAll(resources.links.mixins.extension)
-- see tests/LinkMixinNotInUse.xmi

context Resource
def : configuration : Configuration = oclContainer().oclAsType(Configuration)

context Configuration
inv AllResourcesLinksTargetsInConfiguration:
resources.links.target->forAll(r | r.configuration = self)
-- see tests/LinkTargetNotInConfiguration.xmi

context Entity
inv AttributesNameUnique:
attributes->isUnique(name)
-- TODO: a test case



--
-- Other constraints not really necessary?
--

--
-- Constraints related to Category.term.
--

context Extension
inv TermUnicity:
  kinds.term->intersection(mixins.term)->isEmpty()
  -- see tests/NoTermUnicity.xmi

context Kind
inv ActionTermUnicity:
  actions->isUnique(term)
  -- see tests/NoKindActionTermUnicity.xmi

context Mixin
inv ActionTermUnicity:
  actions->isUnique(term)
  -- see tests/NoMixinActionTermUnicity.xmi

--
-- Constraints related to Category.scheme.
--

context Kind
inv CorrectScheme:
  scheme = extension.scheme
  -- see tests/InvalidKindScheme.xmi

context Mixin
inv CorrectScheme:
  scheme.substring(1,extension.scheme.size()-1) = extension.scheme.substring(1,extension.scheme.size()-1)
  -- see tests/InvalidMixinScheme.xmi

-- context Attribute
-- TODO: Is it needed?
-- inv DefaultMustBeSetWhenRequiredIsTrue: required = true and mutable = false implies default <> null

endpackage
